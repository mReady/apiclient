apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    api 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.2'
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-rx2:1.3.2'
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.2'

    api 'com.squareup.okhttp3:okhttp:4.2.2'
    api 'com.beust:klaxon:5.0.11'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier.set('sources')
}

publishing {
    publications {
        ApiClientPublication(MavenPublication) {
            from components.java
            groupId 'net.mready.apiclient'
            artifactId 'apiclient'
            artifact(sourcesJar)
            version VERSION_NAME
        }
    }
}

bintray {
    user = project.hasProperty('user') ? project.property('user') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('key') ? project.property('key') : System.getenv('BINTRAY_API_KEY')

    publish = true
    publications = ['ApiClientPublication']

    pkg {
        repo = 'maven'
        name = 'apiclient'
        userOrg = 'mready'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://mready.net'

        version {
            name = VERSION_NAME
        }
    }
}

/*
  upload:

  ./gradlew clean build -Puser=USER -Pkey=API_KEY -PdryRun=false bintrayUpload
 */
